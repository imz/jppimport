#!/usr/bin/perl -w

use Getopt::Long;
use RPM::Source::Editor;
use RPM::Source::Macros;
use File::Path;
use strict;
use warnings;

my $release='0.1';
my $verbose=0;
my $outdir='.';
my $changelog='- bootstrap pack of jars created with jppbootstrap script
- temporary package to satisfy circular dependencies';
my $force_cleanup;
my (@skiprequires,@skipprovides);
my (@addprovides,@addrequires,@addobsoletes,$opt_epoch);
my ($altrpm,$altspec);
my ($opt_arch_mode);
my $altgroup='Development/Java';
my $release_suffix='jpp';

my (%ARCH_SOURCE);
my %ARCH_MAP = qw/i386 %ix86 i486 %ix86 i586 %ix86 i686 %ix86 athlon %ix86/;

my $result = GetOptions (
		      "arch-mode"   => \$opt_arch_mode,
		      "changelog=s"   => \$changelog,
		      "outdir=s"   => \$outdir,
		      "release=s"   => \$release,
		      "relsuffix=s"   => \$release_suffix,
		      "alt=s"   => \$altrpm,
		      "parent=s"   => \$altrpm,
		      "epoch=s"   => \$opt_epoch,
		      "group=s"   => \$altgroup,
		      "skip-requires=s"   => \@skiprequires,
		      "skip-provides=s"   => \@skipprovides,
		      "add-provides=s"   => \@addprovides,
		      "add-requires=s"   => \@addrequires,
		      "add-obsoletes=s"   => \@addobsoletes,
		      "verbose+"  => \$verbose);

die unless @ARGV;

my $macros=RPM::Source::Macros->new();
my $packager=$macros->macro_subst('%{packager}');
$packager=undef if $packager eq '%{packager}';

my %skip;
$skip{PROVIDENAME}={map {$_=>1} split(',',join(',',@skipprovides))};
$skip{REQUIRENAME}={map {$_=>1} split(',',join(',',@skiprequires))};

if (not $opt_arch_mode) {
    foreach (@ARGV) {
	die "Oops: $_ not found" unless -e $_;
	&package($_);
    }
} else {
    die "TODO: not implemented: arch mode"
}

sub package {
    my ($donor, $tmpdir, $trippletname, $bootstrap, $SOURCESDIR);
    foreach my $rpmfile (@_) {
	$donor=RPM::Donor::Query->new(RPM=>$rpmfile);
	my $next_trippletname=$donor->query('NAME')."-".$donor->query('VERSION')."-".$donor->query('RELEASE');
	my $arch=$donor->query('ARCH');
	unless ($bootstrap) {
	    $trippletname=$next_trippletname;
	    $tmpdir = $trippletname.$$;
	    $bootstrap=RPM::SpecFile::Bootstrap->new(-tmpdir=>$tmpdir);
	} else {
	    die "tripplet mismatch: $trippletname $next_trippletname" unless $trippletname eq $next_trippletname;
	}
	$SOURCESDIR=$bootstrap->get_sourcesdir();
	`rpm2cpio $rpmfile > $SOURCESDIR/$trippletname.cpio`;
    }

    my $SPECSDIR=$bootstrap->get_specsdir();
    my $specfile=$SPECSDIR."/".$donor->query('NAME').'.spec';
    &write_spec_file($specfile, $donor, $trippletname);
    system ('add_changelog', '-e',$changelog,$specfile)==0 || &cleanup_and_exit($tmpdir);
    mkpath([$outdir]) unless -d $outdir;
    system ('rpmbuild', '--define', '_sourcedir '.$SOURCESDIR, '--define', '_srcrpmdir '.$outdir, '-bs', $specfile)==0 || &cleanup_and_exit($tmpdir);
    &cleanup($tmpdir);
}

sub print_tag {
    my ($fn,$tag,$tagval)=@_;
    print $fn ucfirst(lc($tag)).": ".$tagval."\n" if defined $tagval && $tagval ne '(none)';
}

sub write_spec_file {
    my ($specfile, $donor, $trippletname)=@_;
    my $parent;
    if ($altrpm || $altspec) {
	$parent=RPM::Source::Editor->new(SOURCERPM => $altrpm, SPECFILE => $altspec, VERBOSE=> $verbose);
    }

    open SPEC, '>', $specfile;

    foreach my $tag (qw/
NAME
VERSION
SUMMARY
LICENSE
URL
/) {
    my $tagval=$donor->query($tag);
    &print_tag(\*SPEC, $tag,$tagval);
}

    my $epoch=$donor->query('EPOCH');
    if ($parent) {
	my $parentepoch=$parent->main_section->get_tag('Epoch');
	if (defined $parentepoch and (not defined $epoch or $epoch eq '(none)' or $parentepoch> $epoch)) {
	    $epoch=$parentepoch;
	}
    }
    $epoch=$opt_epoch if defined $opt_epoch;
    &print_tag(\*SPEC, 'EPOCH',$epoch);

    print SPEC "Packager: $packager\n" if $packager;

    my $pkgname=$donor->query('NAME');

    foreach my $triplet (
[qw/PROVIDENAME PROVIDEFLAGS PROVIDEVERSION/]
	) {
	my @tagval=$donor->triquery(@$triplet);
	@tagval=grep {$_!~/^.+\(/ or /^(?:mvn|osgi|npm)\(/} @tagval;
	my $tag=$triplet->[0];
	#print STDERR "DEBUG: $triplet->[0]: ",join(' ',@tagval),"\n";
	my $tagname=ucfirst(lc($tag));
	$tagname=~s/name/s/;
	foreach my $val (@tagval) {
	    print SPEC $tagname.": ".$val."\n" if $val ne $pkgname and not $skip{$tag}->{$val};
	}
    }

    foreach my $tag (qw/
REQUIRENAME
		     /) {
    my @tagval=$donor->multiquery($tag);
    @tagval=grep {$_!~/^.+\(/ or /^mvn\(/ or /^osgi\(/} @tagval;
    #print STDERR "DEBUG: $tag: ",join(' ',@tagval),"\n";
    my $tagname=ucfirst(lc($tag));
    $tagname=~s/name/s/;
    foreach my $val (@tagval) {
	#(npm(sprintf-js) >= 1 with npm(sprintf-js) < 2)
	$val=$1 if $val=~m/^\((\S+)\s/;
	#print STDERR $tagname, ': ', $val, "\n";
	print SPEC $tagname.": ".$val."\n" if $val ne $pkgname and not $skip{$tag}->{$val};
    }
}


    $release='alt'.$release unless $release=~/^alt/;
    $release=$release.$release_suffix if $release_suffix and not $release=~/${release_suffix}$/;
    print SPEC '
BuildArch: noarch
Group: '.$altgroup.'
Release: '.$release.'
Source: '.$trippletname.".cpio\n\n";

    print SPEC 'Provides: '.join(' ',@addprovides)."\n" if @addprovides;
    print SPEC 'Requires: '.join(' ',@addrequires)."\n" if @addrequires;
    print SPEC 'Obsoletes: '.join(' ',@addobsoletes)."\n" if @addobsoletes;

    foreach my $tag (qw/DESCRIPTION
/) {
    my $query = $donor->query($tag);
    print SPEC '%'.lc($tag)."\n".$query."\n" if $query ne '';
}

    print SPEC q!
# sometimes commpress gets crazy (see maven-scm-javadoc for details)
%set_compress_method none
%prep
cpio -idmu --quiet --no-absolute-filenames < %{SOURCE0}

%build
cpio --list < %{SOURCE0} | sed -e 's,^\.,,' > %name-list

%install
mkdir -p $RPM_BUILD_ROOT
for i in usr var etc; do
[ -d $i ] && mv $i $RPM_BUILD_ROOT/
done
!;

#PRETRANS
#POSTTRANS

    foreach my $sec (qw/
PREIN
PREUN
POSTIN
POSTUN
/) {
    my $secname=lc($sec);
    $secname=~s/in$//;
    my $val=$donor->query($sec);
    print SPEC '
%'.$secname."\n".$val."\n" if $val and $val ne "(none)";
}

    print SPEC q!

%files -f %name-list

%changelog
!;
    if ($parent) {
	my @changelog_alt=@{$parent->get_section('changelog')->get_bodyref()};
	shift @changelog_alt; # %changelog header
	print SPEC @changelog_alt;
    }
    close(SPEC);
}

sub cleanup {
    my $tmpdir=shift;
    `rm -r $tmpdir`;
}

sub cleanup_and_exit {
    my $tmpdir=shift;
    &cleanup($tmpdir) if $force_cleanup;
    exit 1;
}

#sub eval {
#    my $tag = shift;
#    #%packager
#    return `rpmquery --eval '$tag' 2>/dev/null`
#}

package RPM::SpecFile::Bootstrap;

use strict;
use warnings;
use File::Path qw(make_path remove_tree);

sub new {
    my $class=shift;
    my $self = {
	-force_cleanup => 0,
	@_,
    };
    my $tmpdir=$self->{-tmpdir};
    unless ($tmpdir) {
	die "Oops! no -tmpdir";
    } else {
	make_path($tmpdir);
    }
    return bless $self, $class;
}

sub get_sourcesdir {
    my $self=shift;
    my $SOURCESDIR=$self->{-sourcesdir};
    unless ($SOURCESDIR) {
	my $tmpdir=$self->{-tmpdir};
	$SOURCESDIR="$tmpdir/SOURCES";
	$self->{-sourcesdir}=$SOURCESDIR;
	mkdir $SOURCESDIR;
    }
    return $SOURCESDIR;
}

sub get_specsdir {
    my $self=shift;
    my $SPECSDIR=$self->{-specsdir};
    unless ($SPECSDIR) {
	my $tmpdir=$self->{-tmpdir};
	$SPECSDIR="$tmpdir/SPECS";
	$self->{-specsdir}=$SPECSDIR;
	mkdir $SPECSDIR;
    }
    return $SPECSDIR;
}

sub cleanup {
    my $self=shift;
    my $tmpdir=$self->{-tmpdir};
    remove_tree($tmpdir);
}

sub cleanup_and_exit {
    my $self=shift;
    my $tmpdir=$self->{-tmpdir};
    &cleanup($tmpdir) if $self->{-force_cleanup};
    exit 1;
}

package RPM::Donor::Query;

use strict;
use warnings;

sub new {
    my $class=shift;
    my $self = {
	@_,
    };
    return bless $self, $class;
}

sub query{
    my $self=shift;
    my $tag=shift;
    my $query=`rpmquery -p --queryformat '\%{$tag}' $self->{RPM}`;
    chomp $query;
    return $query;
}

sub multiquery {
    my $self=shift;
    my $tag=shift;
    return grep {$_} split (/\n/, `rpmquery -p --queryformat '[\%{$tag}\n]' $self->{RPM}`);
}

sub triquery {
    my ($self,$tag1,$tag2,$tag3)=@_;
    my @triplets=grep {$_} split (/\n/, `rpmquery -p --queryformat '[\%{$tag1} %{$tag2} %{$tag3}\n]' $self->{RPM}`);
    my @out;
    foreach my $triplet (@triplets) {
	my @triple=split (/\s+/, $triplet);
	my $rel=&__rpmflag_to_string($triple[1]);
	if ($rel) {
	    push @out, $triple[0].' '.$rel.' '.$triple[2];
	} else {
	    push @out, $triple[0];
	}
    }
    return @out;
}

sub __rpmflag_to_string {
    my $flag=shift;
    $flag||=0;
    $flag &= 0xf;
    return '' if $flag == 0;
    return '<' if $flag == 2;
    return '>' if $flag == 4;
    return '=' if $flag == 8;
    return '<=' if $flag == 10;
    return '>=' if $flag == 12;
    return '';
}

__END__
